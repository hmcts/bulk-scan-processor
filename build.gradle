plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.8.RELEASE'
  id 'org.flywaydb.flyway' version '5.2.4'
  id 'org.springframework.boot' version '2.1.6.RELEASE'
  id 'org.owasp.dependencycheck' version '5.1.1'
  id 'com.github.ben-manes.versions' version '0.21.0'
  id 'org.sonarqube' version '2.7.1'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }

  apiGatewayTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/apiGatewayTest/java')
    }
    resources.srcDir file('src/apiGatewayTest/resources')
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath

  testLogging {
    exceptionFormat = 'full'
  }
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath

  // set your environment variables here
   environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")

  testLogging {
    exceptionFormat = 'full'
  }
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

task apiGateway(type: Test) {
  description = "Runs API Gateway Tests"
  testClassesDirs = sourceSets.apiGatewayTest.output.classesDirs
  classpath = sourceSets.apiGatewayTest.runtimeClasspath
}

checkstyle {
  maxWarnings = 0
  toolVersion = '8.21'
  // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
  configDir = new File(rootDir, 'config/checkstyle')
}

pmd {
  toolVersion = "6.5.0"
  ignoreFailures = true
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSetFiles = files("config/pmd/ruleset.xml")
  ruleSets = []
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
  afterEvaluate {
    getClassDirectories().from = getClassDirectories().files.collect {
      fileTree(dir: it, exclude: [
        'uk/gov/hmcts/reform/bulkscanprocessor/config/**'
      ])
    }
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

sonarqube {
  properties {
    property "sonar.projectName", "Reform :: Bulk Scan Processor"
    property "sonar.coverage.jacoco.xmlReportPaths", jacocoTestReport.reports.xml.destination.path
    property "sonar.exclusions", "**/model/out/*,**/config/**"
  }
}

dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'
  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

dependencyManagement {
  dependencies {
    dependency group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.62'
    // CVE-2018-10237 - Unbounded memory allocation
    dependencySet(group: 'com.google.guava', version: '28.0-jre') {
      entry 'guava'
    }
    // CVE-2019-12814
    dependencySet(group: 'com.fasterxml.jackson.core', version: '2.9.9.1') {
      entry 'jackson-databind'
    }
  }
}

repositories {
  mavenCentral()
  jcenter()

  maven {
    url "https://dl.bintray.com/hmcts/hmcts-maven"
  }
}

// it is important to specify logback classic and core packages explicitly as libraries like spring boot
// enforces it's own (older) version which is not recommended.
def versions = [
  springBoot: springBoot.class.package.implementationVersion,
  springHystrix: '2.1.1.RELEASE',
  springfoxSwagger: '2.9.2'
]

ext["rest-assured.version"] = '4.0.0'

dependencies {
  // exclude spring-cloud-context as it is already included in spring-cloud-starter-netflix-hystrix
  def withoutSpringCloudContext = {
    exclude group: 'org.springframework.cloud', module: 'spring-cloud-context'
  }

  def withoutJavaxMailApi = {
    exclude group: 'javax.mail', module: 'mailapi'
  }

  compile group: 'net.javacrumbs.shedlock', name: 'shedlock-spring', version: '2.5.0'
  compile group: 'net.javacrumbs.shedlock', name: 'shedlock-provider-jdbc', version: '2.5.0'

  compile group: 'com.microsoft.azure', name: 'azure-storage', version: '8.3.0'
  compile group: 'com.microsoft.azure', name: 'azure-servicebus', version: '1.2.14', {
    exclude group: 'javax.mail', module: 'mail'
    exclude group: 'net.minidev', module: 'json-smart'
  }

  compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.springBoot
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: versions.springBoot
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: versions.springBoot
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-json', version: versions.springBoot
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: versions.springBoot
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-mail', version: versions.springBoot

  compile group: 'uk.gov.hmcts.reform', name: 'health-spring-boot-starter', version:'0.0.4'
  compile group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version:'0.0.4', withoutSpringCloudContext

  compile group: 'com.github.java-json-tools', name: 'json-schema-validator', version: '2.2.10', withoutJavaxMailApi

  compile group: 'org.flywaydb', name: 'flyway-core', version: '5.2.4'
  compile group: 'org.postgresql', name: 'postgresql', version: '42.2.6'
  // review following dependency after integrating db structure
  compile group: 'com.vladmihalcea', name: 'hibernate-types-52', version: '2.5.0'

  compile group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
  compile group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger

  compile group: 'uk.gov.hmcts.reform', name: 'java-logging', version: '3.0.4'
  compile group: 'uk.gov.hmcts.reform', name: 'java-logging-appinsights', version: '5.0.1'
  compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix', version: versions.springHystrix

  compile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '3.0.0'
  compile group: 'uk.gov.hmcts.reform', name: 'document-management-client', version: '6.0.0'

  compile group: 'commons-io', name: 'commons-io', version: '2.6'
  compile group: 'io.vavr', name: 'vavr', version: '1.0.0-alpha-2'

  compile group: 'org.apache.commons', name: 'commons-csv', version: '1.7'

  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot
  testCompile group: 'com.jayway.awaitility', name: 'awaitility', version: '1.7.0'
  testCompile group: 'com.icegreen', name: 'greenmail', version: '1.5.10'
  testCompile group: 'org.apache.commons', name: 'commons-email', version: '1.5'

  integrationTestCompile sourceSets.main.runtimeClasspath
  integrationTestCompile sourceSets.test.runtimeClasspath
  integrationTestCompile group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', version: '2.1.1.RELEASE'
  integrationTestCompile group: 'org.testcontainers', name: 'postgresql', version: '1.11.4'
  integrationTestCompile group: 'com.revinate', name: 'assertj-json', version: '1.2.0'

  functionalTestCompile sourceSets.main.runtimeClasspath
  functionalTestCompile sourceSets.test.runtimeClasspath
  functionalTestCompile group: 'io.rest-assured', name: 'rest-assured'
  functionalTestCompile group: 'com.typesafe', name: 'config', version: '1.3.4'
  functionalTestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot

  smokeTestCompile sourceSets.main.runtimeClasspath
  smokeTestCompile sourceSets.test.runtimeClasspath
  smokeTestCompile sourceSets.functionalTest.runtimeClasspath
  smokeTestCompile group: 'io.rest-assured', name: 'rest-assured'

  apiGatewayTestCompile sourceSets.main.runtimeClasspath
  apiGatewayTestCompile sourceSets.test.runtimeClasspath

  apiGatewayTestCompile group: 'io.rest-assured', name: 'rest-assured'
  apiGatewayTestCompile group: 'com.typesafe', name: 'config', version: '1.3.4'
}

mainClassName = 'uk.gov.hmcts.reform.bulkscanprocessor.Application'

bootJar {
  getArchiveFileName().set(provider {
    'bulk-scan-processor.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

import org.flywaydb.gradle.task.FlywayMigrateTask

flyway {
  url = System.getenv('FLYWAY_URL')
  user = System.getenv('FLYWAY_USER')
  password = System.getenv('FLYWAY_PASSWORD')
  baselineOnMigrate = true
  baselineVersion = '000'
}

task migratePostgresDatabase(type: FlywayMigrateTask) {
  baselineOnMigrate = true
  if (project.hasProperty("dburl")) {
    url = "jdbc:postgresql://${dburl}"
  }
}

test {
  testLogging {
    exceptionFormat = 'full'
  }
}

