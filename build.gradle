plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.9.RELEASE'
  id 'org.flywaydb.flyway' version '6.3.2'
  id 'org.springframework.boot' version '2.2.6.RELEASE'
  id 'org.owasp.dependencycheck' version '5.3.2.1'
  id 'com.github.ben-manes.versions' version '0.28.0'
  id 'org.sonarqube' version '2.8'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }

  apiGatewayTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/apiGatewayTest/java')
    }
    resources.srcDir file('src/apiGatewayTest/resources')
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

test {
  failFast = true
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
  useJUnitPlatform()
  testLogging {
    exceptionFormat = 'full'
  }
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath

  // set your environment variables here
   environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")

  testLogging {
    exceptionFormat = 'full'
  }
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

task apiGateway(type: Test) {
  description = "Runs API Gateway Tests"
  testClassesDirs = sourceSets.apiGatewayTest.output.classesDirs
  classpath = sourceSets.apiGatewayTest.runtimeClasspath
}

checkstyle {
  maxWarnings = 0
  toolVersion = '8.29'
  // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
  configDir = new File(rootDir, 'config/checkstyle')
}

pmd {
  toolVersion = "6.5.0"
  ignoreFailures = true
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSetFiles = files("config/pmd/ruleset.xml")
  ruleSets = []
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
  afterEvaluate {
    getClassDirectories().from = getClassDirectories().files.collect {
      fileTree(dir: it, exclude: [
        'uk/gov/hmcts/reform/bulkscanprocessor/config/**'
      ])
    }
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

sonarqube {
  properties {
    property "sonar.projectName", "Reform :: Bulk Scan Processor"
    property "sonar.coverage.jacoco.xmlReportPaths", jacocoTestReport.reports.xml.destination.path
    property "sonar.exclusions", "**/model/out/*,**/config/**"
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'
  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

// it is important to specify logback classic and core packages explicitly as libraries like spring boot
// enforces it's own (older) version which is not recommended.
def versions = [
  junit: '5.5.1',
  junitPlatform: '1.5.1',
  mockitoJupiter: '3.0.0',
  springBoot: springBoot.class.package.implementationVersion,
  springfoxSwagger: '2.9.2'
]

dependencyManagement {
  dependencies {
    dependency group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.65'
    // CVE-2018-10237 - Unbounded memory allocation
    dependencySet(group: 'com.google.guava', version: '28.2-jre') {
      entry 'guava'
    }
    // align with jupiter
    dependencySet(group: 'org.mockito', version: versions.mockitoJupiter) {
      entry 'mockito-core'
    }
  }
}

repositories {
  mavenCentral()
  jcenter()
}

ext["rest-assured.version"] = '4.0.0'

dependencies {
  // exclude spring-cloud-context as it is already included in spring-cloud-starter-netflix-hystrix
  def withoutSpringCloudContext = {
    exclude group: 'org.springframework.cloud', module: 'spring-cloud-context'
  }

  def withoutJavaxMailApi = {
    exclude group: 'javax.mail', module: 'mailapi'
  }

  compile group: 'net.javacrumbs.shedlock', name: 'shedlock-spring', version: '4.6.0'
  compile group: 'net.javacrumbs.shedlock', name: 'shedlock-provider-jdbc', version: '4.7.0'

  compile group: 'com.microsoft.azure', name: 'azure-storage', version: '8.6.3'
  compile group: 'com.microsoft.azure', name: 'azure-servicebus', version: '1.2.18', {
    exclude group: 'javax.mail', module: 'mail'
    exclude group: 'net.minidev', module: 'json-smart'
  }

  compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.springBoot
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: versions.springBoot
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: versions.springBoot
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-json', version: versions.springBoot
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: versions.springBoot
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-mail', version: versions.springBoot

  compile group: 'uk.gov.hmcts.reform', name: 'health-spring-boot-starter', version:'0.0.5'
  compile group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version:'0.0.4', withoutSpringCloudContext

  compile group: 'com.github.java-json-tools', name: 'json-schema-validator', version: '2.2.11', withoutJavaxMailApi

  compile group: 'org.flywaydb', name: 'flyway-core', version: '6.3.2'
  compile group: 'org.postgresql', name: 'postgresql', version: '42.2.12'
  // review following dependency after integrating db structure
  compile group: 'com.vladmihalcea', name: 'hibernate-types-52', version: '2.9.7'

  compile group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
  compile group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger

  compile group: 'uk.gov.hmcts.reform', name: 'java-logging', version: '3.0.4'
  compile group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: '5.1.1'
  compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix', version: '2.2.2.RELEASE'

  compile group: 'io.github.openfeign', name: 'feign-httpclient', version: '10.9'

  compile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '3.1.1'
  compile group: 'uk.gov.hmcts.reform', name: 'document-management-client', version: '6.0.0'

  compile group: 'commons-io', name: 'commons-io', version: '2.6'
  compile group: 'io.vavr', name: 'vavr', version: '1.0.0-alpha-3'

  compile group: 'org.apache.commons', name: 'commons-csv', version: '1.8'

  testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
  testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
  testCompile group: 'org.junit.platform', name: 'junit-platform-commons', version: versions.junitPlatform
  testCompile group: 'org.junit.platform', name: 'junit-platform-engine', version: versions.junitPlatform
  testCompile group: 'org.mockito', name: 'mockito-junit-jupiter', version: versions.mockitoJupiter
  testCompile group: 'io.github.netmikey.logunit', name: 'logunit-logback', version: '1.1.0'

  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot
  testCompile group: 'com.jayway.awaitility', name: 'awaitility', version: '1.7.0'
  testCompile group: 'com.icegreen', name: 'greenmail', version: '1.5.12'
  testCompile group: 'org.apache.commons', name: 'commons-email', version: '1.5'

  integrationTestCompile sourceSets.main.runtimeClasspath
  integrationTestCompile sourceSets.test.runtimeClasspath
  integrationTestCompile group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', version: '2.2.2.RELEASE', {
    exclude group: 'com.github.tomakehurst', module: 'wiremock-jre8-standalone'
  }
  integrationTestCompile group: 'com.github.tomakehurst', name: 'wiremock', version: '2.25.1'
  integrationTestCompile group: 'org.testcontainers', name: 'postgresql', version: '1.13.0'
  integrationTestCompile group: 'com.revinate', name: 'assertj-json', version: '1.2.0'

  functionalTestCompile sourceSets.main.runtimeClasspath
  functionalTestCompile sourceSets.test.runtimeClasspath
  functionalTestCompile group: 'io.rest-assured', name: 'rest-assured'
  functionalTestCompile group: 'com.typesafe', name: 'config', version: '1.4.0'
  functionalTestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot

  smokeTestCompile sourceSets.main.runtimeClasspath
  smokeTestCompile sourceSets.test.runtimeClasspath
  smokeTestCompile sourceSets.functionalTest.runtimeClasspath
  smokeTestCompile group: 'io.rest-assured', name: 'rest-assured'

  apiGatewayTestCompile sourceSets.main.runtimeClasspath
  apiGatewayTestCompile sourceSets.test.runtimeClasspath

  apiGatewayTestCompile group: 'io.rest-assured', name: 'rest-assured'
  apiGatewayTestCompile group: 'com.typesafe', name: 'config', version: '1.3.4'
}

mainClassName = 'uk.gov.hmcts.reform.bulkscanprocessor.Application'

bootJar {
  getArchiveFileName().set(provider {
    'bulk-scan-processor.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

import org.flywaydb.gradle.task.FlywayMigrateTask

flyway {
  url = System.getenv('FLYWAY_URL')
  user = System.getenv('FLYWAY_USER')
  password = System.getenv('FLYWAY_PASSWORD')
  baselineOnMigrate = true
  baselineVersion = '000'
}

task migratePostgresDatabase(type: FlywayMigrateTask) {
  baselineOnMigrate = true
  if (project.hasProperty("dburl")) {
    url = "jdbc:postgresql://${dburl}"
  }
}
