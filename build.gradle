plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.3'
  id 'org.flywaydb.flyway' version '9.20.0'
  id 'org.springframework.boot' version '2.7.17'
  id 'org.owasp.dependencycheck' version '8.4.3'
  id 'com.github.ben-manes.versions' version '0.47.0'
  id 'org.sonarqube' version '4.2.1.3168'
  id "info.solidsoft.pitest" version '1.7.0'
  id 'au.com.dius.pact' version '4.3.15'
}

group = 'uk.gov.hmcts.reform'
version = '0.1.0' // all the time it was 0.0.1. 0.1.0 marks migration to java 11

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

sourceSets {
  contractTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
  }
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
  }

}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

test {
  failFast = true
}

task contract(type: Test, description: 'Runs the contract tests.', group: 'Verification') {
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
}

task runAndPublishConsumerPactTests(type: Test){
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
}

runAndPublishConsumerPactTests.finalizedBy pactPublish

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath

  // set your environment variables here
   environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

task fortifyScan(type: JavaExec)  {
  main = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

checkstyle {
  maxWarnings = 0
  toolVersion = '9.3'
  // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
  configDirectory.set(new File(rootDir, 'config/checkstyle'))
}

pmd {
  toolVersion = "6.55.0"
  ignoreFailures = true
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSetFiles = files("config/pmd/ruleset.xml")
  ruleSets = []
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
  afterEvaluate {
    getClassDirectories().from = getClassDirectories().files.collect {
      fileTree(dir: it, exclude: [
        'uk/gov/hmcts/reform/bulkscanprocessor/config/**'
      ])
    }
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

pitest {
  group = "Verification"
  pitestVersion.set('1.5.1')
  junit5PluginVersion.set('0.15')
  testSourceSets = [sourceSets.test]
  mainSourceSets = [sourceSets.main]
//  targetClasses = ['uk.gov.hmcts.reform.bulkscanprocessor.*']
//  excludedClasses = [
//    'uk.gov.hmcts.reform.bulkscanprocessor.config.*'
//  ]
  threads = 15
  enableDefaultIncrementalAnalysis = true
  historyInputLocation = 'build/reports/pitest/fastermutationtesting'
  historyOutputLocation = 'build/reports/pitest/fastermutationtestingoutput'
  outputFormats = ['XML', 'HTML']
  timestampedReports = false
  mutationThreshold = 0
  coverageThreshold = 0
  failWhenNoMutations = false
  timeoutConstInMillis = 30000
  mutators.set([
    'CONDITIONALS_BOUNDARY',
    'INCREMENTS',
    'INVERT_NEGS',
    'MATH',
    'NEGATE_CONDITIONALS',
    'VOID_METHOD_CALLS',
    'EMPTY_RETURNS',
    'FALSE_RETURNS',
    'NULL_RETURNS',
    'PRIMITIVE_RETURNS',
    'TRUE_RETURNS',
    'REMOVE_CONDITIONALS'
  ])
}

sonarqube {
  properties {
    property "sonar.projectName", "Reform :: Bulk Scan Processor"
    property "sonar.coverage.jacoco.xmlReportPaths", jacocoTestReport.reports.xml.destination.path
    property "sonar.pitest.mode", "reuseReport"
    property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    property "sonar.exclusions", "**/model/out/*,**/config/**,**/LeaseAcquirer.java,**/jms/**"
  }
}

pact {
  publish {
    pactBrokerUrl = 'https://pact-broker.platform.hmcts.net'
    version = '0.0.1'
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'
  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

// it is important to specify logback classic and core packages explicitly as libraries like spring boot
// enforces it's own (older) version which is not recommended.
def versions = [
  junit: '5.9.3',
  junitPlatform: '1.9.3',
  mockitoJupiter: '3.3.3',
  reformLogging: '6.0.1',
  apiguardian     : '1.1.2'
]

dependencyManagement {
  dependencies {
    // CVE-2018-10237 - Unbounded memory allocation
    dependencySet(group: 'com.google.guava', version: '32.0.1-jre') {
      entry 'guava'
    }
    // CVE-2021-22044
    dependencySet(group: 'org.springframework.cloud', version: '3.1.8') {
      entry 'spring-cloud-starter-openfeign'
    }
    // CVE-2022-22970, CVE-2022-22971
    dependencySet(group: 'org.springframework', version: '5.3.28') {
      entry 'spring-aop'
      entry 'spring-aspects'
      entry 'spring-beans'
      entry 'spring-context'
      entry 'spring-context-support'
      entry 'spring-core'
      entry 'spring-expression'
      entry 'spring-jcl'
      entry 'spring-jdbc'
      entry 'spring-orm'
      entry 'spring-tx'
      entry 'spring-web'
      entry 'spring-webmvc'
    }
    dependencySet(group: 'org.apache.logging.log4j', version: '2.20.0') {
      entry 'log4j-api'
      entry 'log4j-to-slf4j'
    }
    //CVE-2022-23181
    dependencySet(group: 'org.apache.tomcat.embed', version: '9.0.83') {
      entry 'tomcat-embed-core'
      entry 'tomcat-embed-websocket'
    }
    // CVE-2020-36518
    dependencySet(group: 'com.fasterxml.jackson.core', version: '2.13.4.2') {
      entry 'jackson-databind'
    }
    // CVE-2021-42550
    dependencySet(group: 'ch.qos.logback', version: '1.2.10') {
      entry 'logback-classic'
    }
    //CVE-2022-24823
    dependencySet(group: 'io.netty', version: '4.1.99.Final') {
      entry 'netty-buffer'
      entry 'netty-codec'
      entry 'netty-codec-dns'
      entry 'netty-codec-http'
      entry 'netty-codec-http2'
      entry 'netty-codec-socks'
      entry 'netty-common'
      entry 'netty-handler'
      entry 'netty-handler-proxy'
      entry 'netty-resolver'
      entry 'netty-resolver-dns'
      entry 'netty-resolver-dns-classes-macos'
      entry 'netty-resolver-dns-native-macos'
      entry 'netty-transport'
      entry 'netty-transport-classes-epoll'
      entry 'netty-transport-classes-kqueue'
      entry 'netty-transport-native-epoll'
      entry 'netty-transport-native-kqueue'
      entry 'netty-transport-native-unix-common'
    }
    // CVE-2022-25857
    dependencySet(group: 'org.yaml', version: '2.0') {
      entry 'snakeyaml'
    }
    // CVE-2023-24998
    dependencySet(group: 'commons-fileupload', version: '1.5') {
      entry 'commons-fileupload'
    }
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

ext.libraries = [
  junit5: [
    "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
    "org.junit.platform:junit-platform-commons:${versions.junitPlatform}",
    "org.junit.platform:junit-platform-engine:${versions.junitPlatform}",
    "org.apiguardian:apiguardian-api:${versions.apiguardian}"
  ]
]

dependencies {

  def withoutJavaxMailApi = {
    exclude group: 'javax.mail', module: 'mailapi'
  }

  implementation group: 'net.javacrumbs.shedlock', name: 'shedlock-spring', version: '5.5.0'
  implementation group: 'net.javacrumbs.shedlock', name: 'shedlock-provider-jdbc', version: '5.5.0'

  implementation group: 'com.azure', name: 'azure-storage-blob', version: '12.21.1'

  implementation group: 'com.azure', name: 'azure-messaging-servicebus', version: '7.14.1'

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-mail'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-activemq'

  implementation group: 'org.apache.qpid', name: 'qpid-jms-client', version: '1.9.0'

  implementation group: 'com.github.java-json-tools', name: 'json-schema-validator', version: '2.2.14', withoutJavaxMailApi

  implementation group: 'org.flywaydb', name: 'flyway-core', version: '7.15.0'
  implementation group: 'org.postgresql', name: 'postgresql', version: '42.6.0'
  // review following dependency after integrating db structure
  implementation group: 'com.vladmihalcea', name: 'hibernate-types-52', version: '2.21.1'

  implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.7.0'

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: versions.reformLogging
  implementation group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: versions.reformLogging
  implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '7.4'
  implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '4.1.2'
  implementation group: 'com.github.hmcts', name: 'idam-java-client', version: '3.0.1'
  implementation group: 'com.github.hmcts', name: 'ccd-case-document-am-client', version: '1.59'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: '3.1.7'
  implementation group: 'io.github.openfeign', name: 'feign-httpclient', version: '12.3'

  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: '3.1.7'
  implementation group: 'io.github.openfeign', name: 'feign-httpclient', version: '12.3'

  implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.15.2'

  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
  implementation group: 'commons-io', name: 'commons-io', version: '2.13.0'
  // only used in uk.gov.hmcts.reform.bulkscanprocessor.validation.OcrValidator
  // single import: io.vavr.control.Try
  implementation group: 'io.vavr', name: 'vavr', version: '0.10.4'

  implementation group: 'org.apache.commons', name: 'commons-csv', version: '1.10.0'
  implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.1.6'


  testImplementation libraries.junit5
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
  testImplementation group: 'com.jayway.awaitility', name: 'awaitility', version: '1.7.0'
  testImplementation group: 'com.icegreen', name: 'greenmail', version: '1.6.14', {
    exclude group: 'junit', module: 'junit'
  }
  testImplementation group: 'org.apache.commons', name: 'commons-email', version: '1.5'
  testImplementation group: 'io.github.netmikey.logunit', name: 'logunit-core', version: '2.0.0'
  testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: '1.3.0', classifier: 'all', {
    exclude group: 'commons-io', module: 'commons-io'
    exclude group: 'org.apache.commons', module: 'commons-lang3'
  }
  testRuntimeOnly group: 'io.github.netmikey.logunit', name: 'logunit-logback', version: '2.0.0'

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath
  integrationTestImplementation group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', version: '3.1.8', {
    exclude group: 'com.github.tomakehurst', module: 'wiremock-jre8-standalone'
  }
  integrationTestImplementation group: 'com.github.tomakehurst', name: 'wiremock', version: '2.33.2'
  integrationTestImplementation group: 'org.testcontainers', name: 'postgresql', version: '1.18.3', {
    exclude group: 'junit', module: 'junit'
  }
  integrationTestImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: '1.18.3'
  integrationTestImplementation group: 'com.revinate', name: 'assertj-json', version: '1.2.0'

  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation sourceSets.smokeTest.runtimeClasspath

  smokeTestImplementation sourceSets.main.runtimeClasspath
  smokeTestImplementation sourceSets.test.runtimeClasspath
  smokeTestImplementation group: 'io.rest-assured', name: 'rest-assured'
  functionalTestImplementation group: 'com.typesafe', name: 'config', version: '1.4.2'

  contractTestImplementation sourceSets.main.runtimeClasspath
  contractTestImplementation sourceSets.test.runtimeClasspath
  contractTestImplementation group: 'io.rest-assured', name: 'rest-assured'
  contractTestImplementation group: 'au.com.dius', name: 'pact-jvm-consumer-junit5', version: '4.0.10'
}

mainClassName = 'uk.gov.hmcts.reform.bulkscanprocessor.Application'

bootJar {
  getArchiveFileName().set(provider {
    'bulk-scan-processor.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

import org.flywaydb.gradle.task.FlywayMigrateTask

flyway {
  url = System.getenv('FLYWAY_URL')
  user = System.getenv('FLYWAY_USER')
  password = System.getenv('FLYWAY_PASSWORD')
  baselineOnMigrate = true
  baselineVersion = '000'
}

task migratePostgresDatabase(type: FlywayMigrateTask) {
  baselineOnMigrate = true
  if (project.hasProperty("dburl")) {
    url = "jdbc:postgresql://${dburl}"
  }
}
